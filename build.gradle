/**
   Copyright 2016-2019, Adam L. Davis

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/
/** -------------------------> Build works using Gradle 5.4.1 and Java 8.0.202 */
plugins {
    id "org.groocss.groocss-gradle-plugin" version "1.0-M4-groovy2.5"
    id 'org.jbake.site' version '5.0.0'
    id "java"
    id "groovy"
}
def cssDir = "stylesheets/"

groocss {
    charset = 'UTF-8'
    prettyPrint = true
    convertUnderline = true
    processors = []
}
groocssfiles {
    allfiles {
        inFile = file('src/main/groovy/')
        outFile = file("$cssDir")
    }
}

ext {
    groocssVer = "1.0-M4-groovy2.5"
}
configurations {
    sources
}
repositories {
    mavenLocal()
    jcenter()
}
dependencies {
    compile "org.groocss:groocss:$groocssVer"
    sources "org.groocss:groocss:$groocssVer:sources"
}
sourceSets {
    main {
        groovy {
            srcDir(file("sources"))
        }
    }
}
jbake {
    srcDirName = 'src/jbake/'
    destDirName = '.'
    version = '2.6.4'
    groovyTemplatesVersion = '2.5.5'
    clearCache = true
}

task copySite(type: Copy) {
    from 'build/'
    into './'
    exclude '**/*.bak'
}

task site(dependsOn: ['convertCss', 'bake', 'copySite'], description: 'Builds the site') doLast {
    println 'done'
}

task buildDocs(type: Groovydoc, dependsOn: 'unzipSources', description: 'Builds the groovy docs') {
    destinationDir = file("docs/$groocssVer")
    docTitle = "GrooCSS $groocssVer"
    classpath = sourceSets.main.output + sourceSets.main.compileClasspath
    link 'https://docs.oracle.com/javase/8/docs/api/', 'java.util', 'java.lang', 'java.awt', 'java.math', 'java.io'
    link 'http://docs.groovy-lang.org/latest/html/api/', 'groovy.lang', 'groovy.transform', 'groovy.json'
    source "sources"
}

task unzipSources(type: Copy, description: 'Unzips all the source files') {
    def zipPath = project.configurations.sources.find { it.name.startsWith('groo') }
    def zipFile = file(zipPath)
    def outDir = file("sources")

    from zipTree(zipFile)
    into outDir
}

task beforeCss doLast {
    file(cssDir).mkdirs()
	println 'Processing groocss'
}
tasks['convertCss'].dependsOn beforeCss

