apply plugin: "groovy"
apply plugin: "application"
apply plugin: 'maven'

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

applicationName = "groocss"
mainClassName= 'org.groocss.GrooCSS'
version="0.2.1"

task zipDocs(type: Jar) {
    dependsOn 'groovydoc'
    archiveName "$applicationName-${version}-javadocs.jar"
    from "$buildDir/docs/groovydoc/"
}
task zipSrc(type: Jar) {
    archiveName "$applicationName-${version}-sources.jar"
    from "src/main/groovy/"
}
task jars {
    dependsOn('zipDocs', 'zipSrc')
}

task pom << {
    pom {
        project {
            packaging 'jar'
            groupId 'org.groocss'
            inceptionYear '2016'
            url 'http://groocss.org/'
            name 'GrooCSS'
            description 'the missing piece for full-stack Groovy; write compile-time checked CSS in Groovy'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    name 'Adam L. Davis'
                    email 'adamd@adamldavis.com'
                    organization 'The Solution Design Group, Inc.'
                    organizationUrl 'http://thesdg.com/'
                }
            }
            scm {
                connection 'scm:git:git://github.com/adamldavis/groocss.git'
                developerConnection 'scm:git:ssh://git@github.com:adamldavis/groocss.git'
                url 'https://github.com/adamldavis/groocss'
            }
        }
    }.writeTo("$buildDir/$applicationName-${version}.pom")
}
