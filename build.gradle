plugins {
    id "groovy"
    id "application"
    id 'maven'
    id "com.jfrog.bintray" version "1.7"
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

applicationName = "groocss"
mainClassName= 'org.groocss.GrooCSS'

task zipDocs(type: Jar) {
    dependsOn 'groovydoc'
    archiveName "$applicationName-${version}-javadocs.jar"
    from "$buildDir/docs/groovydoc/"
}
task zipSrc(type: Jar) {
    archiveName "$applicationName-${version}-sources.jar"
    from "src/main/groovy/"
}
task jars {
    dependsOn 'zipDocs', 'zipSrc', 'jar'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'GrooCSS'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/adamldavis/groocss.git'
        version {
            name = project.version
            released  = new Date()
        }
        filesSpec {
            from "$project.buildDir/libs", {
                include '*.jar'
            }
            from "$project.buildDir/", {
                include '*.pom'
            }
            into "org/groocss/groocss/$project.version"
        }
    }
}

task buildRelease(dependsOn: ['test', 'jars', 'pom']) {}

task release(dependsOn: ['bintrayUpload']) << {
    println "Uploaded $project.version to bintray"
    def props = file('gradle.properties')
    def text = props.text
    props.text = "${text.trim()}-SNAPSHOT\n"
}

task prepareRelease(dependsOn: ['clean']) << {
    def props = file('gradle.properties')
    def text = props.text
    props.text = text.replace('-SNAPSHOT','')
}

task pom << {
    pom {
        project {
            groupId 'org.groocss'
            inceptionYear '2016'
            url 'http://groocss.org/'
            name 'GrooCSS'
            description 'Write compile-time checked CSS in Groovy'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    name 'Adam L. Davis'
                    email 'adamd@adamldavis.com'
                }
            }
            scm {
                connection 'scm:git:git://github.com/adamldavis/groocss.git'
                developerConnection 'scm:git:ssh://git@github.com:adamldavis/groocss.git'
                url 'https://github.com/adamldavis/groocss'
            }
        }
    }.writeTo("$buildDir/$applicationName-${version}.pom")
}
