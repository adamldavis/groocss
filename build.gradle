plugins {
    id 'com.gradle.build-scan' version '1.0'
    id "groovy"
    id "application"
    id 'maven'
    id "com.jfrog.bintray" version "1.7.1"
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

project.group = 'org.groocss'
applicationName = "groocss"
mainClassName= 'org.groocss.GrooCSS'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

task zipDocs(type: Jar) {
    dependsOn 'groovydoc'
    archiveName "$applicationName-${version}-javadocs.jar"
    from "$buildDir/docs/groovydoc/"
}
task zipSrc(type: Jar) {
    archiveName "$applicationName-${version}-sources.jar"
    from "src/main/groovy/"
}
task jars {
    dependsOn 'zipDocs', 'zipSrc', 'jar'
}
tasks['jar'].archiveName = "${applicationName}-${project.version}.jar"

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'GrooCSS'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/adamldavis/groocss.git'
        version {
            name = project.version
            released  = new Date()
        }
        filesSpec {
            from "$project.buildDir/libs", {
                include '*.jar'
            }
            from "$project.buildDir/", {
                include '*.pom'
            }
            into "org/groocss/groocss/$project.version"
        }
    }
}

task prepareRelease(dependsOn: clean, description: 'Cleans build and removes -SNAPSHOT') << {
    def props = file('gradle.properties')
    def text = props.text
    props.text = text.replace('-SNAPSHOT','')
}

task buildRelease(dependsOn: ['test', 'jars', 'pom'], description: 'Tests and builds artifacts') {}

task release(dependsOn: bintrayUpload, description: 'Uploads artifacts to bintray maven repo (jcenter)') << {
    println "Uploaded $project.version to bintray"
    incrementVersion()
}

task inc(description: 'Increments the project version and appends -SNAPSHOT') << { incrementVersion() }

def incrementVersion() {
    def props = file('gradle.properties')
    def text = props.text
    def i = text.indexOf('=')
    def nums = text[(i+1)..-1].trim().split(/\./)
    nums[-1] = (nums[-1] as int) + 1
    props.text = "${text[0..i]}${nums.join('.')}-SNAPSHOT\n"
}

task pom << {
    pom {
        project {
            groupId 'org.groocss'
            artifactId 'groocss'
            inceptionYear '2016'
            url 'http://groocss.org/'
            name 'GrooCSS'
            description 'Code CSS in Groovy'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    name 'Adam L. Davis'
                    email 'adamd@adamldavis.com'
                }
            }
            scm {
                connection 'scm:git:git://github.com/adamldavis/groocss.git'
                developerConnection 'scm:git:ssh://git@github.com:adamldavis/groocss.git'
                url 'https://github.com/adamldavis/groocss'
            }
        }
    }.writeTo("$buildDir/$applicationName-${version}.pom")
}
